plugins {
    id 'java'
    id "com.google.protobuf" version "0.8.7" // Protobuf Gradle Plugin
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

def grpcVersion = '1.16.1'
def protoVersion = '3.6.1'

dependencies {
    compile group: 'io.grpc', name: 'grpc-protobuf', version: grpcVersion    
    compile group: 'io.grpc', name: 'grpc-alts', version: grpcVersion    
    compile group: 'com.google.api.grpc', name: 'proto-google-common-protos', version: '1.12.0'
    compile group: 'com.google.protobuf', name: 'protobuf-java-util', version: protoVersion
    compile group: 'io.grpc', name: 'grpc-netty-shaded', version: grpcVersion // Netty-based transport
    compile group: 'io.grpc', name: 'grpc-stub', version: grpcVersion
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protoVersion}" // Protobuf Compiler
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}